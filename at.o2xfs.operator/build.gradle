dependencies {
    compile project(':at.o2xfs.xfs.service')
    testCompile libraries.junit
    releaseDll project(path: ':at.o2xfs.xfs.service', configuration: 'releaseDll')
}

jar {
    manifest {
        attributes(
            "Main-Class": "at.o2xfs.operator.O2XfsOperator",
            "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

task dist(type: Zip) {
    into('lib') {
        from configurations.runtime
        from 'build/libs'
    }
    into('bin') {
        from configurations.releaseDll
    }
    into('config') {
        from 'config'
    }
    from 'start.cmd', 'services.properties'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'at.o2xfs'
            artifactId 'o2xfs-operator'
            version version

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
        zip(MavenPublication) {
            artifact dist
        }
    }

    repositories {
        maven {
            url "https://repo.fagschlunger.co.at/libs-snapshot-local"
            credentials {
                username = project.hasProperty('maven_username') ? project.getProperty('maven_username') : ''
                password = project.hasProperty('maven_password') ? project.getProperty('maven_password') : ''
            }
        }
    }
}

// create the javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// create the sources jar
task sourcesJar(type: Jar, dependsOn: compileJava) {
    from sourceSets.main.allSource
    classifier = 'sources'
}