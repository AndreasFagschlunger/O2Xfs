plugins {
    id 'jvm-component'
    id 'java-lang'
}

apply plugin: 'cpp'
apply plugin: 'windows-resources'

model {
    binaries {
        withType(SharedLibraryBinarySpec) {
            sharedLibraryFile "${project.buildDir}/libs/${component.name}/shared/at.o2xfs.win32.dll"
        }
    }

    repositories {
        libs(PrebuiltLibraries) {
            jni {
                def javaHome = org.gradle.internal.jvm.Jvm.current().javaHome.absolutePath
                headers.srcDirs "${javaHome}/include", "${javaHome}/include/win32"
            }
        }
    }

    components {
        'at.o2xfs.win32'(JvmLibrarySpec) {
            sources {
                java {
                    source.srcDir 'src/main/java'
                }
            }
            dependencies {
                project ':at.o2xfs.common'
                project ':at.o2xfs.log'
                module libraries.commons_lang3
            }
        }

        'native'(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDir 'src/native/cpp'
                    }
                    exportedHeaders {
                        srcDir 'src/native/headers'
                    }
                }
                rc {
                    source {
                        srcDir 'src/native/rc'
                    }
                }
            }

            binaries.all {
                lib library: "jni", linkage: "api"
                rcCompiler.args "/v"
                rcCompiler.define "FILE_VERSION=\"0.0\""
                rcCompiler.define "COMMA_VERSION=0,0,0,0"
                rcCompiler.define "ORG_FILENAME="
            }
        }
    }
}